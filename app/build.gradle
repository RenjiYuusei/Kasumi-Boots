plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.kasumi.boots'
    compileSdk 35

    defaultConfig {
        applicationId "com.kasumi.boots"
        minSdk 24
        targetSdk 35
        versionCode 1
        versionName "1.0.0"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            def props = new Properties()
            def propsFile = rootProject.file("keystore.properties")
            if (propsFile.exists()) {
                props.load(new FileInputStream(propsFile))
                storeFile props.containsKey('storeFile') ? rootProject.file(props['storeFile']) : null
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            } else {
                println "keystore.properties not found. Release signing will fail on CI if not provided."
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.activity:activity-ktx:1.9.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def libsuVersion = '6.0.0'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"
}
